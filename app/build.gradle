apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def getApplicationId = { ->
    return project.hasProperty('applicationId') ? applicationId : "com.trevorhalvorson.portal"
}

def getVersionCode = { ->
    return project.hasProperty('versionCode') ? versionCode.toInteger() : 1
}

def getVersionName = { ->
    return project.hasProperty('versionName') ? versionName : "1.0.0"
}

def props = project.getProperties()

def url, enableWebViewLogs, allowBrowsing, allowFileUpload, toolbarTitle

task(configureApp) {
    url = props.get("url")
    enableWebViewLogs = props.get("enableWebViewLogs")
    allowBrowsing = props.get("allowBrowsing")
    allowFileUpload = props.get("allowFileUpload")
    toolbarTitle = props.get("toolbarTitle")
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId getApplicationId()
        minSdkVersion 23
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField("String", "URL", "$url")
            buildConfigField("Boolean", "ENABLE_WEB_VIEW_LOGS", "$enableWebViewLogs")
            buildConfigField("Boolean", "ALLOW_BROWSING", "$allowBrowsing")
            buildConfigField("Boolean", "ALLOW_FILE_UPLOAD", "$allowFileUpload")
            buildConfigField("String", "TOOLBAR_TITLE", "$toolbarTitle")
        }
        debug {
            buildConfigField("String", "URL", '"https://www.wikipedia.org/"')
            buildConfigField("Boolean", "ENABLE_WEB_VIEW_LOGS", "true")
            buildConfigField("Boolean", "ALLOW_BROWSING", "true")
            buildConfigField("Boolean", "ALLOW_FILE_UPLOAD", "false")
            buildConfigField("String", "TOOLBAR_TITLE", '"PORTAL DEBUG"')
        }
    }
    lintOptions {
        abortOnError true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
}
